plugins {
    id 'org.springframework.boot' version '2.1.5.RELEASE'
}

ext {
    javaVersion = JavaVersion.VERSION_1_8
    lombokVersion = '1.18.6'
    junitJupiterVersion = '5.2.0'
}

def javaLibraries   = [ project(':watchdog-core'), project(':watchdog-browser'), project(':watchdog-native'),
                        project(':watchdog-browser-starter-spring-boot')],
    emptyProjects   = [ project(':samples') ],
    bootProjects    = subprojects - javaLibraries - emptyProjects

allprojects {
    group = 'io.watchdog'
    version = '1.0-SNAPSHOT'
}

configure(javaLibraries) {
    apply plugin: 'java-library'
    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:2.1.5.RELEASE')
    }
}

configure(bootProjects) {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    dependencies {
        implementation 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
    }
}

configure(javaLibraries + bootProjects) {
    sourceCompatibility = javaVersion
    compileJava {
        options.encoding = 'UTF-8'
    }    
    
    apply plugin: 'idea'
    // requires to delegate IDEA build/run actions to Gradle
    idea {
        module {
            sourceDirs += sourceSets.main.output.generatedSourcesDirs
            generatedSourceDirs += sourceSets.main.output.generatedSourcesDirs
            testSourceDirs += sourceSets.test.output.generatedSourcesDirs
            generatedSourceDirs += sourceSets.test.output.generatedSourcesDirs
        }
    }

    repositories {
        maven {url = 'http://maven.aliyun.com/nexus/content/groups/public/'}
        mavenCentral()
    }
    apply plugin: "io.spring.dependency-management"
    dependencies {
        implementation platform('org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE')
        
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
        testImplementation ("org.springframework.boot:spring-boot-starter-test") {
            exclude module: 'junit'
        }
        
        implementation 'org.apache.commons:commons-lang3:3.8'
        implementation 'org.apache.commons:commons-collections4:4.2'
        implementation 'commons-beanutils:commons-beanutils:1.9.3'
        implementation 'com.google.guava:guava:27.1-jre'
    }

    test {
        useJUnitPlatform()
    }
}


