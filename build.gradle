buildscript {
    repositories {
        maven {url = 'https://plugins.gradle.org/m2/'}
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.8.RELEASE'
    }
}

ext {
    javaVersion = JavaVersion.VERSION_1_8
    lombokVersion = '1.18.2'
    junitJupiterVersion = '5.2.0'
}

def javaLibraries = [project(':watchdog-core'), project(':watchdog-browser'), project(':watchdog-native'), 
                     project(':watchdog-browser-starter-spring-boot')]
def emptyProjects = [project(':samples')]
def bootProjects = subprojects - javaLibraries - emptyProjects

configure(javaLibraries) {
    apply plugin: 'java-library'
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    compileJava {
        options.encoding = 'UTF-8'
    }
}

configure(bootProjects) {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    bootRun {
        sourceResources sourceSets.main
    }
    dependencies {
        implementation 'org.springframework.boot:spring-boot-devtools'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
    }
}

configure(subprojects - emptyProjects) {
    group = 'io.watchdog'
    version = '1.0-SNAPSHOT'
    apply plugin: "io.spring.dependency-management"
    afterEvaluate {
        repositories {
            maven {url = 'http://maven.aliyun.com/nexus/content/groups/public/'}
        }
        dependencyManagement {
            imports {
                mavenBom "io.spring.platform:platform-bom:Cairo-SR7"
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE"
            }
        }
    }
    dependencies {
        compileOnly "org.projectlombok:lombok:${this.lombokVersion}"
        
        testImplementation "org.junit.jupiter:junit-jupiter-api:${this.junitJupiterVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${this.junitJupiterVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${this.junitJupiterVersion}"
        testImplementation ("org.springframework.boot:spring-boot-starter-test") {
            exclude module: 'junit'
        }
        
        implementation 'org.apache.commons:commons-lang3:3.8'
        implementation 'org.apache.commons:commons-collections4:4.2'
        implementation 'commons-beanutils:commons-beanutils:1.9.3'
    }

    test {
        useJUnitPlatform()
    }
}

